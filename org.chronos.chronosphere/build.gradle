// ORG.CHRONOS.CHRONOSPHERE/build.gradle

plugins {
    id 'org.jetbrains.kotlin.jvm'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    manifest {
        attributes 'Implementation-Title': 'ChronoSphere'
    }
}

test {
    filter {
        includeTestsMatching "org.chronos.chronosphere.test.cases.*"
    }
    useJUnit {
        // exclude performance tests and benchmarks
        excludeCategories 'org.chronos.common.test.junit.categories.PerformanceTest'
        // exclude all suites (they are redundant when working with gradle)
        excludeCategories 'org.junit.runners.Suite'
    }
}

dependencies {
    api project(':org.chronos.common');
    api project(':org.chronos.chronodb.api');
    api project(':org.chronos.chronograph');

    api("org.eclipse.emf:org.eclipse.emf.ecore:2.11.1-v20150805-0538")
    api("org.eclipse.emf:org.eclipse.emf.common:2.11.0-v20150805-0538")
    api("org.eclipse.emf:org.eclipse.emf.ecore.xmi:2.11.1-v20150805-0538")

    api("org.apache.tinkerpop:gremlin-core:${project.tinkerpopVersion}")

    testImplementation project(':org.chronos.common.testing')
    testImplementation project(':org.chronos.common').sourceSets.test.output;
    testImplementation project(':org.chronos.chronodb.api').sourceSets.test.output;
    testImplementation project(':org.chronos.chronograph').sourceSets.test.output;
    testImplementation("org.apache.tinkerpop:gremlin-test:${project.tinkerpopVersion}")

    testImplementation("io.strikt:strikt-core:${project.striktVersion}")

    // we also add the backend-specific modules to the test dependencies so that
    // we can have all tests in the same module (i.e. AllBackendsTest)
    testImplementation project(':org.chronos.chronodb.exodus')
}
