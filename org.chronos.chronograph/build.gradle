// ORG.CHRONOS.CHRONOGRAPH/build.gradl

plugins {
    id 'org.jetbrains.kotlin.jvm'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    manifest {
        attributes 'Implementation-Title': 'ChronoGraph'
    }
}

dependencies {
    api project(":org.chronos.common")
    api project(":org.chronos.chronodb.api")

    api("org.apache.tinkerpop:gremlin-core:${project.tinkerpopVersion}")
    api("org.codehaus.groovy:groovy:${project.groovyVersion}")

    implementation("io.github.microutils:kotlin-logging-jvm:${project.kotlinLoggingVersion}")
    api("jakarta.annotation:jakarta.annotation-api:${project.jakartaAnnotations}")

    testImplementation project(":org.chronos.common.testing")
    testImplementation project(':org.chronos.common').sourceSets.test.output;
    testImplementation project(':org.chronos.chronodb.api').sourceSets.test.output;

    // the official tinkerpop test suite
    testImplementation("org.apache.tinkerpop:gremlin-test:${project.tinkerpopVersion}")

    testImplementation("io.strikt:strikt-core:${project.striktVersion}")


    // we also add the backend-specific modules to the test dependencies so that
    // we can have all tests in the same module (i.e. AllBackendsTest)
    testImplementation project(':org.chronos.chronodb.exodus')
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier "sources"
}

afterEvaluate { project ->

    // Defines the to-be published artifacts with the maven-publish task
    // Is not responsible for any publishing. Just defines the artifacts.
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourceJar
                //the maven descriptor info
                groupId project.group
                artifactId jar.baseName
                version project.mavenVersion
            }
        }
    }
}

test {
    filter {
        includeTestsMatching "org.chronos.chronograph.test.cases.*"
        includeTestsMatching "org.chronos.chronograph.test._gremlinsuite.*"
    }
    useJUnit {
        // exclude performance tests and benchmarks
        excludeCategories 'org.chronos.common.test.junit.categories.PerformanceTest'
        // exclude all suites (they are redundant when working with gradle)
        excludeCategories 'org.junit.runners.Suite'
    }
}

