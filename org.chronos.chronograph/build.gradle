// ORG.CHRONOS.CHRONOGRAPH/build.gradl

plugins {
    id 'org.jetbrains.kotlin.jvm'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    manifest {
        attributes 'Implementation-Title': 'ChronoGraph'
    }
}

dependencies {
    compile project(":org.chronos.common")
    compile project(":org.chronos.chronodb.api")

    compile("org.apache.tinkerpop:gremlin-core:${project.tinkerpopVersion}")
    compile("org.codehaus.groovy:groovy:${project.groovyVersion}")


    testCompile project(":org.chronos.common.testing")
    testCompile project(':org.chronos.common').sourceSets.test.output;
    testCompile project(':org.chronos.chronodb.api').sourceSets.test.output;

    // the official tinkerpop test suite
    testCompile("org.apache.tinkerpop:gremlin-test:${project.tinkerpopVersion}")

    // we also add the backend-specific modules to the test dependencies so that
    // we can have all tests in the same module (i.e. AllBackendsTest)
    testCompile project(':org.chronos.chronodb.exodus')
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier "sources"
}

afterEvaluate { project ->

    // Defines the to-be published artifacts with the maven-publish task
    // Is not responsible for any publishing. Just defines the artifacts.
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourceJar
                //the maven descriptor info
                groupId project.group
                artifactId jar.baseName
                version project.mavenVersion
            }
        }
    }
}

test {
    filter {
        includeTestsMatching "org.chronos.chronograph.test.cases.*"
        includeTestsMatching "org.chronos.chronograph.test._gremlinsuite.*"
    }
    useJUnit {
        // exclude performance tests and benchmarks
        excludeCategories 'org.chronos.common.test.junit.categories.PerformanceTest'
        // exclude all suites (they are redundant when working with gradle)
        excludeCategories 'org.junit.runners.Suite'
    }
}

